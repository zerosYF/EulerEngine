#version 330 core
out vec4 FragColor;

in VS_OUT{
	vec3 FragPos;
	vec3 Normal;
	vec2 TexCoords;
	vec4 FragPosLightSpace;
}fs_in;

uniform sampler2D diffuseTexture;
uniform sampler2D shadowMap;
uniform vec3 lightPos;
uniform vec3 viewPos;

uniform bool shadows;

float ShadowCalculation(vec4 fragPosLightSpace){
	//透视除法
	vec3 projCoords = fragPosLightSpace.xyz/fragPosLightSpace.w;
	//变换到0-1范围
	projCoords = projCoords*0.5+0.5;
	//取得最近点的深度
	float closestDepth = texture(shadowMap,projCoords.xy).r;
	//取得当前片段在光源视角下的深度
	float currentDepth = projCoords.z;
	//检查；
	vec3 normal = normalize(fs_in.Normal);
	vec3 lightDir = normalize(lightPos-fs_in.FragPos);
	float bias = max(0.05*(1.0-dot(normal,lightDir)),0.005);
	//PCF均值;
	float shadow; 
	vec2 texelSize = 1.0/textureSize(shadowMap,0);
	for (int x= -1; x<=1; x++){
		for(int y = -1; y<=1; y++){
			float pcfDepth = texture(shadowMap,projCoords.xy+vec2(x,y)*texelSize).r;
			shadow += currentDepth-bias>pcfDepth?1.0:0.0;
		}
	}
	shadow /= 9.0;
	//光锥外无阴影；
	if(projCoords.z>1.0){
		shadow = 0.0;
	}
	return shadow;
}

void main(){
	vec3 color = texture(diffuseTexture,fs_in.TexCoords).rgb;
	vec3 normal = normalize(fs_in.Normal);
	vec3 lightColor = vec3(0.4);
	//环境光;
	vec3 ambient = 0.2*color;
	//漫反射；
	vec3 lightDir = normalize(lightPos-fs_in.FragPos);
	float diff = max(dot(lightDir,normal),0.0f);
	vec3 diffuse = diff*lightColor;
	//镜面反射；
	vec3 viewDir = normalize(viewPos-fs_in.FragPos);
	float spec = 0.0;
	vec3 halfwayDir = normalize(lightDir+viewDir);
	spec = pow(max(dot(normal,halfwayDir),0.0),64.0);
	vec3 specular = spec *lightColor;
	//阴影计算；
	float shadow = shadows?ShadowCalculation(fs_in.FragPosLightSpace):0.0;
	shadow = min(shadow,0.75);
	vec3 lighting = (ambient+(1.0-shadow)*(diffuse+specular))*color;
	
	FragColor = vec4(lighting,1.0f);
}